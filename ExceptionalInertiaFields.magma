// This function computes all the field extension that can be produced by an exceptional inertial type that becomes triple imprimitive over the cubic extension K_i/Q4, with 0<i<5
// The output consist of 2 object, first a list with a generator for each the three quadratic extensions of M_ij that form a Galois orbit that is a subgroup of order 4, Second a 
// list where the i-th element is a list containing for each triple M_ij all the fields L that are, quadratic over the compositum M of the M_ij  
// and such that L/Q4 has Galois group SL_2(F3).
// See the example at the end for a more concrete explanation.


function Exceptionals(i)

Q2:=pAdicField(2,16);
Q4:=UnramifiedExtension(Q2,2);
Ext:=AllExtensions(Q4,3);
K<s>:=FieldOfFractions(Ext[i]);
Gal,GaltoAut:=AutomorphismGroup(K,Q4);
sigma:=GaltoAut(Gal.2);
SelGr,fK:=pSelmerGroup(2,K);


//We tansform the elements in the gorup SelGr to elements in K
Sel:=[Inverse(fK)(z): z in SelGr];

// This function computes the Orbits that are also subgroups, that is, Galois submodules of the selmer Galois Module  
SubgroupOrbits:=[ ];
OrbitsInSel:=[];
for z in Sel do
    z1:=z;
    z2:=sigma(z);
    z3:=sigma(sigma(z));
    zOrbit:={fK(z1),fK(z2),fK(z3)};
    if zOrbit notin OrbitsInSel then
        if (fK(z1)*fK(z2) eq fK(z3)) and not IsIdentity(fK(z1)) then 
            OrbitsInSel:=Append(OrbitsInSel,{fK(z1),fK(z2),fK(z3)}); // We need this list to check that we are not considering the same orbit twice 
            SubgroupOrbits:=Append(SubgroupOrbits,[z1,z2,z3]);      // We cannot check with the list of elements in K beacuse each element in SelGr has different representatives in K
        end if;
    end if;
end for;

// Now for each orbit we build the composite M of the M_ij, and compute the quadratic extensions of M
// that are Galois over Q4 with Galois group SL2F3.
SL2F3Ext:=[];
for k in [1..#SubgroupOrbits] do
    print("Quadratic extension of K_i number"); k;
    Orbit:=SubgroupOrbits[k];
    R<x>:=PolynomialRing(K);
    E1:=SplittingField(x^2-Orbit[1]);
    R<x>:=PolynomialRing(E1);
    E:=SplittingField(x^2-Orbit[2]);
    R<x>:=PolynomialRing(E); 
    // Since the orbits are subgroups it is superflous to add the root of x^2-z3 so we can stop here. 


    GalE,GalEtoAutE:=AutomorphismGroup(E,Q4);
    GalEGen:=[GalEtoAutE(GalE.j) : j in [2..#Generators(GalE)+1]]; // j starts in 2 because GalE.1 is the identity.
    ESelGr,fE:=pSelmerGroup(2,E);
    ESelGen:=[Inverse(fE)(ESelGr.j) : j in [1..#Generators(ESelGr)]];

    V:=VectorSpace(GF(2),#ESelGen);
    for g in GalEGen do
        G:=[];
        for z in ESelGen do 
            G:=Append(G,ElementToSequence(fE(g(z))));
        end for;
        FixedByG:=Eigenspace(Matrix(GF(2),G),1);
        V:=V meet FixedByG;
    end for;
    
    // Then for each v in V we check if the field L=M(sqrt{v}) has Galois group SL2F3
    // Of course this is computationally heavy and the programme may take some time to finish.
    ExtensionsAux:=[ ];
    for v in V do
        z:=Inverse(fE)(ESelGr!ElementToSequence(v));
        L:=SplittingField(x^2-z);
        if not IsZero(v) and (RamificationDegree(L,E) eq 1) then print("The quadratic unramified extension of M is in V"); end if; // this line checks that the quadratic unramified extension of M is in V and thus half of the extensions we obtain produce the same inertia field.
        // Since it is just a check one can comment the above line to speed up the code.
        if IsIsomorphic(AutomorphismGroup(L,Q4),SL(2,3)) then 
            ExtensionsAux:=Append(ExtensionsAux,L);
        end if;
    end for;
    SL2F3Ext:=Append(SL2F3Ext,ExtensionsAux);
    print("------------------------------------------------------");
end for;
return SL2F3Ext, SubgroupOrbits;
end function;



//Example:
//We compute all inertia fields L corresponding to exceptional inertial types that become triply imprimitive over K_1 with the following command:
SL2F3Ext,SubgroupOrbits:=Exceptionals(1);
//To handle better the output we split it.
// Both SubgroupOrbits and SL2F3 are lists of 5 elements corresponding to the 5 Subgroup Orbits of K_1/(K_1)^2.
// The following comand shows representatives of the three elements in the first Subgroup Orbit of K_1/(K_1)^2
SubgroupOrbit[1];
// The square root of any of these representatives produces a quadratic extension M_1j/K_1 from which the inertial type can be induced. All the possible inertia fields L that 
// come from types induced from M_1j are store in 
#SL2F3Ext[1];
#SL2F3Ext[2];
#SL2F3Ext[3];
#SL2F3Ext[4];
#SL2F3Ext[5];
//Note for instance that SL2F3[4] and SL2F3[5] are empty since there are no exceptional types induced from the quadratic extensions generated by SubgroupOrbits[4] or SubgroupOrbits[5].



